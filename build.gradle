buildscript {
    ext {
        kotlinVersion = '1.5.10'

        minSdkVer = 21
        compileSdkVer = 30
        targetSdkVer = 30

        // core / androidx
        appCompat = '1.3.0'
        coreKtx = '1.5.0'
        activity = '1.2.2'
        fragment = '1.3.4'
        materialComponents = '1.3.0'
        lifecycle = '2.3.1'
        testRunner = '1.3.0'
        annotation = '1.2.0'

        // Android Architecture Components
        compose = '1.0.0-beta08'
        navigation = '2.3.5'
        hiltNavigationCompose = '1.0.0-alpha02'
        room = '2.3.0'

        // KotlinX
        coroutines = '1.4.3'
        serialization = '1.1.0'

        // Coil
        coil = '1.2.2'
        coilCompose = '0.11.1'

        // Dependency Injection
        dagger = '2.36'
        hiltVersion = '1.0.0'

        // Square
        okhttp = '4.9.1'
        okio = '2.10.0'
        retrofit = '2.9.0'
        retrofitSerialization = '0.8.0'

        // testing
        junit = '4.13.2'
        junitExt = '1.1.2'
        espressoCore = '3.3.0'
        kluent = '1.61'
        mockito = '3.9.0'
    }
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0-beta03'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"

        classpath "com.google.dagger:hilt-android-gradle-plugin:$dagger"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlinVersion"
    }
    dependencies {
        classpath 'com.google.gms:google-services:4.3.8'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdkVersion compileSdkVer

                defaultConfig {
                    minSdkVersion minSdkVer
                    targetSdkVersion targetSdkVer
                    consumerProguardFiles 'proguard-rules.pro'
                    vectorDrawables.useSupportLibrary = true
                }

                buildFeatures {
                    compose = true
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                lintOptions {
                    abortOnError false
                }

                kotlinOptions {
                    jvmTarget = '1.8'
                    freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
                }

                composeOptions {
                    kotlinCompilerExtensionVersion compose
                }

                testOptions {
                    unitTests.returnDefaultValues = true
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
